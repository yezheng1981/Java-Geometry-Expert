/*
 * To run JGEX, just use
 *
 * ./gradlew run
 *
 * This is how you should create installer packages for JGEX:
 *
 * ./gradlew package      # build and package the software via the javapackager plugin
 * ./gradlew distZip      # build and package the software as a .zip via the application plugin
 *
 * Prerequisites: You need the msgfmt utility (in your PATH) to have internationalized texts.
 * (It is part of the gettext system.)
 */


buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.github.fvarrui:javapackager:1.7.5'
    }
}

plugins {
    id 'application'
}

apply plugin: 'io.github.fvarrui.javapackager.plugin'

// Global settings:
group = 'io.github.kovzol'
ext.softwareVersion = '0.80'

tasks.withType(JavaCompile).configureEach {
    // The original sources of JGEX use the keyword "var" which is a reserved word in later Java versions.
    // As a workaround, we set compatibility to source = 8
    sourceCompatibility = 1.8
}

// These are default settings based on Gradle's standard layout:
ext.poDir = 'src/main/po'
ext.classesDir = 'build/classes/java/main'
ext.applicationMainClass = 'wprover.GExpert'
version = softwareVersion

tasks.addRule("Pattern: msgFmt_<FILE>Po: Compile <FILE>.po into <FILE>.class.") { String taskName ->
    if (taskName.startsWith('msgFmt_') && taskName.endsWith('Po')) {
        def language = (taskName - 'Po').substring('msgFmt_'.length())
        task(taskName) {
            def input = "${poDir}/${language}.po"
            inputs.file input
            def output = "${classesDir}/i18n/Messages_${language}.class"
            outputs.file output
            doLast {
                def msgFmtCmd = "msgfmt --java2 -d ${classesDir} -r i18n.Messages -l ${language} ${input}"
                exec {
                    ignoreExitValue true
                    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
                        commandLine 'cmd', '/c', msgFmtCmd
                    } else {
                        commandLine 'bash', '-c', msgFmtCmd
                    }
                }
            }
        }
    }
}

tasks.register("msgFmtAll") {
    description "Creates the .class files from the .po translations."
    group "build"
    def list = []
    FileTree files = fileTree(dir: poDir)
    files.visit { f ->
        if (f.name.endsWith('.po')) {
            def msgFmtAllTask = 'msgFmt_' + f.name - '.po' + 'Po'
            list << msgFmtAllTask
        }
    }
    dependsOn list
}
tasks.named("classes") { finalizedBy("msgFmtAll") }

tasks.addRule("Pattern: msgMerge_<FILE>Po: Merge the file keys.pot into <FILE>.po.") { String taskName ->
    if (taskName.startsWith('msgMerge_') && taskName.endsWith('Po')) {
        def language = (taskName - 'Po').substring('msgMerge_'.length())
        task(taskName) {
            def input = "${poDir}/keys.pot"
            inputs.file input
            def output = "${poDir}/${language}.po"
            outputs.file output
            doLast {
                def msgFmtCmd = "msgmerge -U ${output} ${input}"
                exec {
                    ignoreExitValue true
                    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
                        commandLine 'cmd', '/c', msgFmtCmd
                    } else {
                        commandLine 'bash', '-c', msgFmtCmd
                    }
                }
            }
        }
    }
}

tasks.register("msgMergeAll") {
    description "Updates the .po files from the .pot template."
    group "translation"
    def list = []
    FileTree files = fileTree(dir: poDir)
    files.visit { f ->
        if (f.name.endsWith('.po')) {
            def msgMergeAllTask = 'msgMerge_' + f.name - '.po' + 'Po'
            list << msgMergeAllTask
        }
    }
    dependsOn list
}


distributions {
    main {
        distributionBaseName = 'jgex'
        contents {
            into("bin") { from 'src/docs'}
        }
    }
}


repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.xmlgraphics:batik-anim:1.17',
            'org.apache.xmlgraphics:batik-awt-util:1.17',
            'org.apache.xmlgraphics:batik-bridge:1.17',
            'org.apache.xmlgraphics:batik-constants:1.17',
            'org.apache.xmlgraphics:batik-css:1.17',
            'org.apache.xmlgraphics:batik-dom:1.17',
            'org.apache.xmlgraphics:batik-ext:1.17',
            'org.apache.xmlgraphics:batik-gui-util:1.17',
            'org.apache.xmlgraphics:batik-gvt:1.17',
            'org.apache.xmlgraphics:batik-i18n:1.17',
            'org.apache.xmlgraphics:batik-parser:1.17',
            'org.apache.xmlgraphics:batik-script:1.17',
            'org.apache.xmlgraphics:batik-svg-dom:1.17',
            'org.apache.xmlgraphics:batik-swing:1.17',
            'org.apache.xmlgraphics:batik-util:1.17',
            'org.apache.xmlgraphics:batik-xml:1.17',
            'org.apache.xmlgraphics:xmlgraphics-commons:2.9',
            'com.googlecode.gettext-commons:gettext-commons:0.9.8',
            'org.graphper:graph-support:1.1.5',
            'ch.qos.logback:logback-core:1.2.9',
            'org.slf4j:slf4j-simple:2.0.7',
            'xml-apis:xml-apis:1.4.01',
            'xml-apis:xml-apis-ext:1.3.04'
}

application {
    mainClass = applicationMainClass
}

jar {
    manifest {
        attributes(
                'Main-Class': application.mainClass
        )
    }
    mustRunAfter "msgFmtAll"
}

run {
    mustRunAfter "msgFmtAll"
    File runningDir = new File('src/docs')
    workingDir = runningDir
}

javapackager {
    description = "JGEX combines dynamic geometry software, automated geometry theorem prover with a visually dynamic approach for presenting proofs."
    mainClass = applicationMainClass
    bundleJre = true
    additionalResources = [ file ("src/docs/examples"), file ("src/docs/help") ]
    organizationName = "JGEX Contributors"
    url = "https://github.com/kovzol/Java-Geometry-Expert"
    organizationEmail = "jgex@googlegroups.com"
    version = softwareVersion
    // fileAssociations = ["application/vnd.geometry-expert", "gex", "Geometry Expert File" ] // FIXME: use correct syntax

    winConfig.generateMsi = false
    winConfig.setupLanguages.english = "compiler:Default.isl"
    // winConfig.setupLanguages.spanish = "compiler:Languages\\Spanish.isl" // TODO: Create the Spanish translation.
    winConfig.setupLanguages.german = "compiler:Languages\\German.isl"
    winConfig.setupLanguages.italian = "compiler:Languages\\Italian.isl"
    winConfig.setupLanguages.portuguese = "compiler:Languages\\Portuguese.isl"
    // See https://github.com/HeliumProject/InnoSetup/tree/master/Languages
    winConfig.disableFinishedPage = false
    winConfig.disableRunAfterInstall = false

    linuxConfig.categories = ["Education", "Graphics", "Science"]
}
